apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.nikolaykul.waveadvance"
        minSdkVersion 16
        targetSdkVersion 23

        // Generate versionCode and versionName. Print them as well
        def longVersionName = "git -C ${projectDir} describe --tags --long".execute().text.trim()
        if (longVersionName) {
            def (fullVersionTag, versionBuild, gitSha) = longVersionName.tokenize('-')
            def (versionMajor, versionMinor, versionPatch) = fullVersionTag.tokenize('.')
            versionName "$versionMajor.$versionMinor.$versionPatch"
            versionCode versionMajor.toInteger() * 10000000 +
                    versionMinor.toInteger() * 100000 +
                    versionPatch.toInteger() * 1000 +
                    versionBuild.toInteger()
        } else {
            versionName "1.0.0"
            versionCode 10000000
        }

        printf("\n--------VERSION DATA--------\n- CODE: " + versionCode +
                "\n- NAME: " + versionName + "\n----------------------------\n")
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        def APP_NAME = "Wave Advance"
        dev {
            applicationIdSuffix ".dev"
            buildConfigField 'boolean', 'ENABLE_LOGGING', 'true'
            resValue "string", "app_name", APP_NAME + " Develop"
        }
        prod {
            buildConfigField 'boolean', 'ENABLE_LOGGING', 'false'
            resValue "string", "app_name", APP_NAME
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.3.0'
}
